; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OneVPN"
#define MyAppVersion "3.04"
#define MyAppPublisher "OneVPN"
#define MyAppURL "http://onevpn.co/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{c85d75cf-5d75-498b-8d52-de3bb7b914e0}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
;OutputDir=C:\Work\LiquidVPN\installer
OutputBaseFilename=onevpn_3_04
;SetupIconFile=VPNVantage.ico
UninstallDisplayIcon={app}\uninstall.ico
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "uninstall.ico"; DestDir: "{app}"
Source: "..\install\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "tap-windows-9.21.1.exe"; DestDir: "{app}"; AfterInstall: RunTapInstaller

[Icons]
Name: "{commondesktop}\OneVPN"; Filename: "{app}\OneVPN.exe"; WorkingDir: "{app}"; Tasks: desktopicon
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; IconFilename: "{app}\uninstall.ico"
Name: "{group}\OneVPN"; Filename: "{app}\OneVPN.exe"; WorkingDir: "{app}"; IconFilename: "{app}\OneVPN.exe"; IconIndex: 0

[Run]
Filename: "sc"; Parameters: "create OneVPNService binPath= ""{app}\OneVPNService.exe"""
Filename: "sc"; Parameters: "sdset OneVPNService ""D:(A;;CCLCSWRPWPDTLOCRRC;;;SY)(A;;CCDCLCSWRPWPDTLOCRSDRCWDWO;;;BA)(A;;CCLCSWLOCRRC;;;IU)(A;;CCLCSWLOCRRC;;;SU)(A;;RPWPCR;;;{code:GetUserSid})S:(AU;FA;CCDCLCSWRPWPDTLOCRSDRCWDWO;;;WD)"""
;Filename: "{app}\subinacl"; Parameters: "/SERVICE OneVPNService /grant={username}=F"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

;[UninstallRun]
;Filename: {sys}\taskkill.exe; Parameters: "/f /im VPNVantage.exe"; Flags: skipifdoesntexist runhidden

[Code]

#include "services_unicode.iss"

function GetUserSid(ExecStdout: string):string;
var
  TmpFileName: string;
  ResultCode: integer;
  Params: string;
  whiteSpacePos: integer;
  sid: string;

  begin
    TmpFileName := ExpandConstant('{tmp}') + '\sid_results.txt';
    Exec(ExpandConstant('{cmd}'), ' /C whoami /user /nh > "' + TmpFileName + '"', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);

    if LoadStringFromFile(TmpFileName, ExecStdout) then 
      begin
        whiteSpacePos := pos (' ', ExecStdout);
        sid := copy(ExecStdout, whiteSpacePos + 1, length(ExecStdout) - 1)    
        {MsgBox(ExecStdout, mbInformation, MB_OK);}
        result := Trim(sid)
      end
      else
        begin
        MsgBox('Installing Error', mbError, MB_OK);
        end;
    DeleteFile(TmpFileName);
end;

procedure RunTapInstaller;
var
  ResultCode: Integer;
begin
  if not Exec(ExpandConstant('{app}\tap-windows-9.21.1.exe'), '', '', SW_SHOWNORMAL,
    ewWaitUntilTerminated, ResultCode)
  then
    MsgBox('Tap drivers installer failed to run!' + #13#10 +
      SysErrorMessage(ResultCode), mbError, MB_OK);
end;


procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  case CurUninstallStep of
    usUninstall:
      begin
      end;
    usPostUninstall:
      begin
      end;
  end;
end;